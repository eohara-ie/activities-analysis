<analytic>
	<analytic>parseTCX</analytic>
	<code_text>\d .aa

\P 20 // High number of significant figures in AltitudeMeters column
CURRENT_DIR:system"dir" //! Windows-only. To include OS check before this point
system"cd C:\\Users\\eohara\\Documents\\fitbit" //! Windows-only


//
// @desc Parses a TCX file.
//
// @param fName     {symbol}    TCX file name.
//
// @return          {table}     Telemetry data.
//
// @example .aa.transform[`exercise_tcx_file_5.tcx]
//
transform:{[fName]
    parsedFile0:.xml.rdXML[read0`$":",string fName];

    pub:`time xcols `altitudeMetres`distanceMetres`heartRateBpm`time`latitudeDegrees`longitudeDegrees xcol 
        delete Position from update "F"$AltitudeMeters,"F"$DistanceMeters,HeartRateBpm:{"H"$first x}each HeartRateBpm,LatitudeDegrees:"F"${first x}each Position,
        LongitudeDegrees:"F"${last x}each Position,Time:{$[count[x]in 24 29;.aa.parseStringToTS x;'"Unknown timestamp format: ",x]}each Time,updateTS:.z.p 
        from first value first((enlist first first last first parsedFile0)`Lap)`Track;
    .msg.pub[`activityTelemetry;pub];
    };


//
// @desc Parses a stringed timestamp from a TCX file. Will throw an error if format does not match one of the examples below.
//
// @param x   {string}        Stringed timestamp.
//
// @return     {timestamp}     Parsed timestamp.
//
// @example     q).aa.parseStringToTS each("2019-01-15T12:17:09.000-05:00";"2019-01-15T12:17:09.000+05:00";"2019-01-15T12:17:09.000Z")
//              2019.01.15D17:17:09.000000000 2019.01.15D07:17:09.000000000 2019.01.15D12:17:09.000000000
//
parseStringToTS:{
    $["Z"=last x;
        .qdate.resolveAs[`timestamp;"%FT%T.%i";x];
        .qdate.resolveAs[`timestamp;"%FT%T.%i%z";(-3_x),-2#x]
        ]
    };


</code_text>
	<description></description>
	<dictionaryparams>0</dictionaryparams>
	<typ>Instruction</typ>
	<private>1</private>
	<returntype></returntype>
	<returndata></returndata>
	<defaultconnection></defaultconnection>
	<alias></alias>
	<analytictype>polling</analytictype>
	<returndescription></returndescription>
</analytic>
